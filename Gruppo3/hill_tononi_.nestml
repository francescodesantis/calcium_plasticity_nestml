
neuron hill_tononi:
    state:
    
        potassium_refr_t ms = 0 ms
        g_spike boolean = false
        rho_GB real = 0
        
        #------------------- Stato correnti/potenziale -------------------------

        V_m mV = ( g_NaL * E_Na + g_KL * E_K ) / ( g_NaL + g_KL ) # membrane potential
        Theta mV = Theta_eq # Threshold
        IKNa_D, IT_m, IT_h, Ih_m nS = 0.0 nS
        
        #----------------- Stato concentrazioni iniziali -----------------------
        
        concentration_Ca_int mmol/dm**3 = 0.00007 mmol/dm**3
        integrated_conc_Ca mmol/dm**3 = 0 mmol/dm**3
        
        #-------------- Stato canali attivazione/inattivazione VDCC ------------
        
        I_VDCC_h real = 0
        I_VDCC_m real = 0
        
        #----------------- Stato conduttanze Neurotrasmettitori ----------------
        
        g_AMPA real = 0
        g_NMDA real = 0.2
        g_GABAA real = 0
        g_GABAB real = 0
        g_AMPA$ real = AMPAInitialValue
        g_NMDA$ real = NMDAInitialValue
        g_GABAA$ real = GABA_AInitialValue
        g_GABAB$ real = GABA_BInitialValue
        damiano real = 0
        
        #----------------------- conduttanza di picco AMPA ----------------------
        
        AMPA_g_peak nS = 0.1 nS

    equations:
        
        #---------------------- Calcolo correnti sinaptiche ---------------------
        
        inline Pf real = 4 * concentration_Ca_ext / (4 * concentration_Ca_ext + (Permeability_M / Permeability_Ca) * concentration_M )
        inline s_concentration_Ca real = alpha * Pf

        inline I_syn_ampa pA = -convolve(g_AMPA, AMPA) * nS * ( V_m - AMPA_E_rev )
        inline I_syn_nmda pA = -convolve(g_NMDA, NMDA) * nS * ( V_m - NMDA_E_rev ) / ( 1 + exp( ( NMDA_Vact - V_m ) / NMDA_Sact ) )
        inline I_syn_gaba_a pA = -convolve(g_GABAA, GABA_A) * nS * ( V_m - GABA_A_E_rev )
        inline I_syn_gaba_b pA = -convolve(g_GABAB, GABA_B) * nS * ( V_m - GABA_B_E_rev )
        inline I_syn pA = I_syn_ampa + I_syn_nmda + I_syn_gaba_a + I_syn_gaba_b
        recordable inline I_NMDA_Ca pA = I_syn_nmda * ( ( 1 + exp( ( NMDA_Vact - V_m ) / NMDA_Sact ) ) / ( V_m - AMPA_E_rev ) ) * s_concentration_Ca * ( V_m - NMDA_Ca_rev )

        inline I_Na pA = -g_NaL * ( V_m - E_Na )
        inline I_K pA = -g_KL * ( V_m - E_K )
        

        #-------------------------- Calcolo corrente Na -------------------------
        
        
        inline INaP_thresh mV = -55.7 mV
        inline INaP_slope mV = 7.7 mV
        inline m_inf_NaP real = 1.0 / ( 1.0 + exp( -( V_m - INaP_thresh ) / INaP_slope ) )
        
        recordable inline I_NaP pA = -NaP_g_peak * m_inf_NaP**3 * ( V_m - NaP_E_rev )
        
        
        #------------------------- Calcolo corrente K-Na ------------------------
        

        inline d_half real = 0.25
        inline m_inf_KNa real = 1.0 / ( 1.0 + ( d_half / ( IKNa_D / nS ) )**3.5 )
        
        recordable inline I_KNa pA = -KNa_g_peak * m_inf_KNa * ( V_m - KNa_E_rev )
        
        
        #--------------------- Calcolo correnti Ca - Canali T --------------------
        

        inline m_inf_T real = 1.0 / ( 1.0 + exp( -( V_m / mV + 59.0 ) / 6.2 ) )
        inline h_inf_T real = 1.0 / ( 1.0 + exp( ( V_m / mV + 83.0 ) / 4 ) )
        inline tau_m_T real = 0.22 / ( exp( -( V_m / mV + 132.0 ) / 16.7 ) + exp( ( V_m / mV + 16.8 ) / 18.2 ) ) + 0.13
        inline tau_h_T real = 8.2 + ( 56.6 + 0.27 * exp( ( V_m / mV + 115.2 ) / 5.0 ) ) / ( 1.0 + exp( ( V_m / mV + 86.0 ) / 3.2 ) )
        
        IT_m' = ( m_inf_T * nS - IT_m ) / tau_m_T / ms
        IT_h' = ( h_inf_T * nS - IT_h ) / tau_h_T / ms
        
        recordable inline I_T pA = -T_g_peak / nS * IT_m / nS * IT_m / nS * IT_h * ( V_m - T_E_rev )
        
        
        #------------------- Calcolo correnti canali H ---------------------------
        
        
        inline I_h_Vthreshold real = -75.0
        inline tau_m_h real = 1.0 / ( exp( -14.59 - 0.086 * V_m / mV ) + exp( -1.87 + 0.0701 * V_m / mV ) )
        inline m_inf_h real = 1.0 / ( 1.0 + exp( ( V_m / mV - I_h_Vthreshold ) / 5.5 ) )
        Ih_m' = ( m_inf_h * nS - Ih_m ) / tau_m_h / ms
        
        recordable inline I_h pA = -h_g_peak / nS * Ih_m  * ( V_m - h_E_rev )
        
        
        #----------------- Calcolo correnti canali VDCC --------------------------
        
        
        inline m_inf_VDCC real = 1.0 / ( 1.0 + exp( ( V_hm - V_m ) / k_m ))
        inline h_inf_VDCC real = 1.0 / ( 1.0 + exp( ( V_hh - V_m ) / k_h ))
        
        I_VDCC_m' = ( m_inf_VDCC - I_VDCC_m ) / tau_m_VDCC
        I_VDCC_h' = ( h_inf_VDCC - I_VDCC_h ) / tau_h_VDCC
        
        recordable inline I_VDCC pA = -VDCC_g_peak * I_VDCC_m * I_VDCC_m * I_VDCC_h * (V_m - VDCC_E_rev)
        
        
        #---------------- Calcolo correnti canali Na-K ---------------------------
        
        
        inline D_influx_peak real = 0.025
        inline tau_D real = 1250.0 # yes, 1.25 s
        inline D_thresh mV = -10.0 mV
        inline D_slope mV = 5.0 mV
        inline D_influx real = 1.0 / ( 1.0 + exp( -( V_m - D_thresh ) / D_slope ) )
        Theta' = -( Theta - Theta_eq ) / Tau_theta
        
        IKNa_D' = ( D_influx_peak * D_influx * nS - ( IKNa_D  - KNa_D_EQ / mV ) / tau_D ) / ms
        
        
        #------------- Calcolo potenziale di membrana ----------------------------
        
        
        inline I_spike mV = (g_spike) ? -( V_m - E_K ) / Tau_spike * ms : 0 mV
        V_m'  = ( ( I_VDCC + I_Na + I_K + I_syn + I_NaP + I_KNa + I_T + I_h + I_e + I_stim ) / Tau_m + I_spike * pA/(ms * mV) ) * s/nF
        
        
        #------------- Emissione spike per pot.soprasoglia -----------------------
        
        
        # potassium_refr_t' = -1e3 * ms/s
        
        
        #--------------- Calcolo concentrazione di Ca e C_star -------------------
        
        
        inline tau_star ms = 278.318 ms
        #inline tau_star ms = 21 ms #Rate of removal of calcium, Sabatini et al 2002
        
        concentration_Ca_int' = ( I_NMDA_Ca + I_VDCC + I_T ) * nu / ( 2 * Fc * X ) - ( concentration_Ca_int - concentration_Ca_int_rest ) / tau_Ca
        integrated_conc_Ca' = - integrated_conc_Ca / tau_star + ( concentration_Ca_int - concentration_Ca_int_rest )/(mmol/dm**3)
        
        
        #--------------------- Dinamica canali Ionici ----------------------------
        
        kernel g_AMPA' = g_AMPA$ - g_AMPA  / AMPA_Tau_2,
               g_AMPA$' = -g_AMPA$ / AMPA_Tau_1

        kernel g_NMDA' = g_NMDA$ - g_NMDA / NMDA_Tau_2,
               g_NMDA$' = -g_NMDA$ / NMDA_Tau_1

        kernel g_GABAA' = g_GABAA$ - g_GABAA / GABA_A_Tau_2,
               g_GABAA$' = -g_GABAA$ / GABA_A_Tau_1

        kernel g_GABAB' = g_GABAB$ - g_GABAB / GABA_B_Tau_2,
               g_GABAB$' = -g_GABAB$ / GABA_B_Tau_1

        #----------------- Aggiornamento conduttanza di picco AMPA ---------------
        
        
        recordable inline AMPA_g_plasticity nS = AMPA_g_depression + rho * (AMPA_g_potentiation - AMPA_g_depression)
        AMPA_g_peak' = ( AMPA_g_plasticity + AMPA_g_peak ) / tau_change
        
        
        #-------------------------- Calcolo di rho -------------------------------
        
        
        inline Hpot real = 1 / (1 + exp(- 2000*(integrated_conc_Ca - theta_p_GB)))
        inline Hdep real = 1 / (1 + exp(- 2000*(integrated_conc_Ca - theta_d_GB)))
        rho_GB' = ( - rho_GB*(1 - rho_GB)*(0.5 - rho_GB) + Hpot * gamma_p_GB * (1 - rho_GB) - Hdep * gamma_d_GB*rho_GB ) / (tau_ind_GB * 1000)
        
        
        
        
        

    parameters:
        
        #tau_star ms = 278318 ms
        
        #--------------------- Potenziale di membrana ---------------------------
        
        E_Na mV = 30.0 mV           # potenziale di Nerst Na
        E_K mV = -90.0 mV           # potenziale di Nerst K
        g_NaL nS =  0.2 nS          # conduttanza sodio
        g_KL nS = 1.0 nS            # conduttanza potassio 1.0 - 1.85
        Tau_m ms = 16.0 ms          # membrane time constant applying to all currents but repolarizing K-current (see [1, p 1677])
        Theta_eq mV = -51.0 mV      # equilibrium value
        Tau_theta ms = 2.0 ms       # time constant
        Tau_spike ms = 1.75 ms      # membrane time constant applying to repolarizing K-current
        t_spike ms = 2.0 ms         # duration of re-polarizing potassium current
        
        #---------------------------- Canali AMPA ---------------------------------
        
        AMPA_E_rev mV = 0.0 mV       # reversal potential
        AMPA_Tau_1 ms = 0.5 ms       # rise time
        AMPA_Tau_2 ms = 2.4 ms       # decay time, Tau_1 < Tau_2
        
        #---------------------------- Canali NMDA ---------------------------------
        
        NMDA_g_peak nS = 0.075 nS    # peak conductance
        NMDA_Tau_1 ms = 4.0 ms       # rise time
        NMDA_Tau_2 ms = 40.0 ms      # decay time, Tau_1 < Tau_2
        NMDA_E_rev mV = 0.0 mV       # reversal potential
        NMDA_Vact mV = -58.0 mV      # inactive for V << Vact, inflection of sigmoid
        NMDA_Sact mV = 2.5 mV        # scale of inactivation
        
        #---------------------------- Canali GABA-A -------------------------------
        
        GABA_A_g_peak nS = 0.33 nS   # peak conductance
        GABA_A_Tau_1 ms = 1.0 ms     # rise time
        GABA_A_Tau_2 ms = 7.0 ms     # decay time, Tau_1 < Tau_2
        GABA_A_E_rev mV = -70.0 mV   # reversal potential
        
        #---------------------------- Canali GABA-B -------------------------------
        
        GABA_B_g_peak nS = 0.0132 nS # peak conductance
        GABA_B_Tau_1 ms = 60.0 ms    # rise time
        GABA_B_Tau_2 ms = 200.0 ms   # decay time, Tau_1 < Tau_2
        GABA_B_E_rev mV = -90.0 mV   # reversal potential for intrinsic current
        
        #---------------------------- Canali VDCC ---------------------------------
        
        VDCC_g_peak nS = 10 nS       # peak conductance for VDCC
        tau_m_VDCC ms = 1 ms         # costante di tempo canali di attivazione VDCC
        tau_h_VDCC ms = 27 ms        # costante di tempo canali di inattivazione VDCC
        VDCC_E_rev mV = 120 mV       # potenziale di inversione canali VDCC
        
        V_hm mV = -5.9 mV            # soglia canale di attivazione VDCC
        k_m mV = 9.5 mV              #
        V_hh mV = -39 mV             # soglia canale di inattivazione VDCC
        k_h mV = -9.2 mV             #
        
        #------------------ Parametri calcolo concentrazioni Ca -------------------
        
        nu real = 0.04                                                # proporzione di ioni Ca liberi
        Fc C/mol = 96485 C/mol                                        # costante di Faraday
        X um**3 = 0.2 um**3                                           # volume spina dendridica
        tau_Ca ms = 12 ms                                             # costante di decadimento concentrazione Ca
        concentration_M mmol/dm**3 = 150 mmol/dm**3                   # concentrazione ioni monovalenti extracellulare
        Permeability_Ca real = 10                                     # permeabilita Ca canali NMDA
        Permeability_M real = 1                                       # permeabilita ioni monovalenti NMDA
        concentration_Ca_ext mmol/dm**3 = 2.0 mmol/dm**3              # concentrazione extracellulare di Ca
        alpha real = 0.6                                              # 
        NMDA_Ca_rev mV = 40 mV                                        # potenziale di inversione Ca nei canali NMDA
        concentration_Ca_int_rest mmol/dm**3 = 0.00007 mmol/dm**3     # concentrazione intracellulare Ca a riposo
        
        #------------- Parametri Aggiornamento conduttanze canali AMPA ------------
        
        AMPA_g_depression nS = 0.08 nS                                # conduttanza AMPA depressi
        AMPA_g_potentiation nS = 0.16 nS                              # conduttanza AMPA potenziati
        tau_change ms = 100000 ms                                     # costante di decadimento calcio integrato
        rho real = 0.5                                                # efficenza sinaptica(sinapsi?)
        
        #----------------- Parameters for intrinsic currents -----------------------
        
        NaP_g_peak nS = 1.0 nS       # peak conductance for intrinsic current
        NaP_E_rev mV = 30.0 mV       # reversal potential for intrinsic current
        KNa_g_peak nS = 1.0 nS       # peak conductance for intrinsic current
        KNa_E_rev mV = -90.0 mV      # reversal potential for intrinsic current
        T_g_peak nS = 1.0 nS         # peak conductance for intrinsic current
        T_E_rev mV = 0.0 mV          # reversal potential for intrinsic current
        h_g_peak nS = 1.0 nS         # peak conductance for intrinsic current
        h_E_rev mV = -40.0 mV        # reversal potential for intrinsic current
        KNa_D_EQ pA = 0.001 pA

        #------------------ Constant external input current -------------------------
        
        I_e pA = 0 pA
        
        #----------------------------- rho_GB ---------------------------------------
        
        theta_d_GB mmol/dm**3 = 0.001 mmol/dm**3 #choosing the correct thresholds gives an error in rho computation 
        theta_p_GB mmol/dm**3 = 0.006 mmol/dm**3
        tau_ind_GB s = 70 s
        gamma_d_GB real = 400 
        gamma_p_GB real = 450

    internals:
        AMPAInitialValue real = compute_synapse_constant( AMPA_Tau_1, AMPA_Tau_2, AMPA_g_peak )
        NMDAInitialValue real = compute_synapse_constant( NMDA_Tau_1, NMDA_Tau_2, NMDA_g_peak )

        GABA_AInitialValue real = compute_synapse_constant( GABA_A_Tau_1, GABA_A_Tau_2, GABA_A_g_peak )
        GABA_BInitialValue real = compute_synapse_constant( GABA_B_Tau_1, GABA_B_Tau_2, GABA_B_g_peak )

    input:
        AMPA <- spike
        NMDA <- spike
        GABA_A <- spike
        GABA_B <- spike
        I_stim pA <- continuous

    output:
        spike 

    update:
        
        integrate_odes()
        
        # Decremento costante di potassium_refr_t
        if potassium_refr_t > 0 ms:
            potassium_refr_t = potassium_refr_t - 0.005 * ms
        else:
            potassium_refr_t = 0 ms  # Mantieni il valore a 0 se scende sotto
            
        #modifiche al file per compatibilita versioni:
            
        if potassium_refr_t <= 0 ms:
            # Deactivate potassium current after spike time have expired
            g_spike = false # Deactivate potassium current.
        
        if(not g_spike and V_m >= Theta) :
            # Set V and Theta to the sodium reversal potential.
            V_m = E_Na
            Theta = E_Na

            # Activate fast potassium current. Drives the
            # membrane potential towards the potassium reversal
            # potential (activate only if duration is non-zero).
            if potassium_refr_t >= 0 ms:
                g_spike = true
            else:
                g_spike = false

            potassium_refr_t = t_spike

            emit_spike()
            

    function compute_synapse_constant(Tau_1 ms, Tau_2 ms, g_peak real) real:
        # Factor used to account for the missing 1/((1/Tau_2)-(1/Tau_1)) term
        # in the ht_neuron_dynamics integration of the synapse terms.
        # See: Exact digital simulation of time-invariant linear systems
        # with applications to neuronal modeling, Rotter and Diesmann,
        # section 3.1.2.
        exact_integration_adjustment real = ( ( 1 / Tau_2 ) - ( 1 / Tau_1 ) ) * ms

        t_peak real = ( Tau_2 * Tau_1 ) * ln( Tau_2 / Tau_1 ) / ( Tau_2 - Tau_1 ) / ms
        normalisation_factor real = 1 / ( exp( -t_peak / Tau_1 ) - exp( -t_peak / Tau_2 ) )

        return g_peak * normalisation_factor * exact_integration_adjustment